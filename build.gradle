plugins {
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id "io.codearte.nexus-staging" version "0.21.1"
    id "org.xbib.gradle.plugin.asciidoctor" version "1.5.6.0.1"
    id "com.github.spotbugs" version "4.2.4"
    id "pmd"
    id "checkstyle"
}

wrapper {
    gradleVersion = "${rootProject.property('gradle.wrapper.version')}"
    distributionType = Wrapper.DistributionType.ALL
}

ext {
    user = 'xbib'
    name = 'content'
    description = 'Content processing library for Java'
    inceptionYear = '2016'
    url = 'https://github.com/' + user + '/' + name
    scmUrl = 'https://github.com/' + user + '/' + name
    scmConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
    scmDeveloperConnection = 'scm:git:ssh://git@github.com:' + user + '/' + name + '.git'
    issueManagementSystem = 'Github'
    issueManagementUrl = ext.scmUrl + '/issues'
    licenseName = 'The Apache License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
}

subprojects {
    apply plugin: 'java-library'
    apply from: rootProject.file('gradle/ide/idea.gradle')
    apply from: rootProject.file('gradle/compile/java.gradle')
    apply from: rootProject.file('gradle/test/junit5.gradle')
    apply from: rootProject.file('gradle/quality/checkstyle.gradle')
    apply from: rootProject.file('gradle/quality/pmd.gradle')
    apply from: rootProject.file('gradle/quality/spotbugs.gradle')
    apply from: rootProject.file('gradle/publishing/publication.gradle')
}
apply from: rootProject.file('gradle/publishing/sonatype.gradle')
